buildscript {
    ext.kotlin_version = '1.2.51'
    ext.wrapper_version = '1.3.33'
    ext.ace_version = '1.3.3'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.33'
    }
}

subprojects {
    group 'ace-wrapper'
    version "$wrapper_version"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    ext.setupNode = {

        task copyClasses(type: Copy) {
            from 'build/classes/kotlin/main'
            into 'build/npm'
        }

        task copySources(type: Copy) {
            from '.'
            into 'build/npm'
            exclude 'build'
            exclude 'build.gradle'
        }

        task expandPackage(type: Copy) {
            from '.'
            into 'build/npm'
            include 'package.json'
            expand(
                    wrapper_version: "$wrapper_version",
                    ace_version: "$ace_version",
                    kotlin_version: "$kotlin_version"
            )
        }

        copyClasses.dependsOn(build)
        expandPackage.dependsOn(copyClasses)
        expandPackage.dependsOn(copySources)
        install.dependsOn(expandPackage)
    }

}